/*
 * Copyright (c) 2015. Netflix, Inc.
 *
 *     Licensed under the Apache License, Version 2.0 (the "License");
 *     you may not use this file except in compliance with the License.
 *     You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *     Unless required by applicable law or agreed to in writing, software
 *     distributed under the License is distributed on an "AS IS" BASIS,
 *     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *     See the License for the specific language governing permissions and
 *     limitations under the License.
 */

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.netflix.nebula:nebula-dependency-recommender:3.1.0'
  }
}

plugins {
  id 'nebula.netflixoss' version '3.1.2'
  id 'me.champeau.gradle.jmh' version '0.2.0'
}

ext {
  githubProjectName = 'servo'
}

allprojects {
  apply plugin: 'nebula.dependency-recommender'
  apply plugin: 'me.champeau.gradle.jmh'
}

subprojects {
  apply plugin: 'nebula.netflixoss'
  apply plugin: 'java'
  apply plugin: 'checkstyle'
  apply plugin: 'findbugs'
  apply plugin: 'pmd'

  group = "com.netflix.${githubProjectName}"

  repositories {
    jcenter()
  }

  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'

  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
  }

  test {
    useTestNG()
    options.excludeGroups = ['aws']
    testLogging.showStandardStreams = true
  }

  task(testWithCloud, type: Test) {
    useTestNG()
    group = 'verification'
    options.excludeGroups = [] as Set
  }

  dependencyRecommendations {
    propertiesFile file: new File(rootProject.projectDir, 'dependencies.properties')
  }

  dependencies {
    compile 'org.slf4j:slf4j-api'
    compile 'com.google.guava:guava'
    testCompile 'org.testng:testng:6.1.1'
    testRuntime 'org.slf4j:slf4j-log4j12'
    testRuntime 'log4j:log4j:1.2.17'
    jmh 'org.slf4j:slf4j-simple'
  }

  jmh {
    warmupIterations = 2
    iterations = 10
    fork = 5
    profilers = ['STACK']
    include '.*'
  }

  checkstyle {
    ignoreFailures = false 
    toolVersion = '6.0'
    configFile = rootProject.file('codequality/checkstyle.xml')
    sourceSets = [sourceSets.main]
  }

  findbugs {
    ignoreFailures = false 
    sourceSets = [sourceSets.main]
  }

  tasks.withType(FindBugs) {
    findbugs.toolVersion "3.0.1"
    reports {
      xml.enabled = false
      html.enabled = true
    }
  }

  pmd {
    ignoreFailures = false
    ruleSets = []
    ruleSetFiles = rootProject.files("codequality/pmd.xml")
    sourceSets = [sourceSets.main]
  }

  project.tasks.withType(Javadoc) {
    if (JavaVersion.current().isJava8Compatible()) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
  javadoc {
    options.addStringOption("sourcepath", "")
  }
}

